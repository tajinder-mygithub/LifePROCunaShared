000010 IDENTIFICATION  DIVISION.
000020 PROGRAM-ID.     MUNAME00.
000030 AUTHOR.         Gaurav Mishra.
000040 DATE-WRITTEN.   22 Dec 2016.
000050 DATE-COMPILED.
000060*@**20160902*******************************************
000070*@** 
000080*@** Licensed Materials - Property of
000090*@** ExlService Holdings, Inc.
000100*@**  
000110*@** (C) 1983-2010 ExlService Holdings, Inc.  All Rights Reserved.
000120*@** 
000130*@** Contains confidential and trade secret information.  
000140*@** Copyright notice is precautionary only and does not
000150*@** imply publication.
000160*@** 
000170*@**20160902*******************************************
000180
000190*D****************************************************************
000200*D                 PROGRAM DESCRIPTION
000210*D****************************************************************
000220*D
000230*D   This program moves the current email address from PADDR
000240*D   to PNAME table.
000260*D
000270*D****************************************************************
000280*H****************************************************************
000290*H        PROGRAM HISTORY
000300*H****************************************************************
000310*H
000320*H 20161201-003-01 GAM New Program
000330*H
000340******************************************************************
000350 ENVIRONMENT DIVISION.
000360 CONFIGURATION SECTION.
000370 SOURCE-COMPUTER. IBM-AT.
000380 OBJECT-COMPUTER. IBM-AT.
000390 SPECIAL-NAMES.
000400
000410 INPUT-OUTPUT SECTION.
000420 FILE-CONTROL.
000430
000440     SELECT EXTRACT-FILE
000450 COPY WVARYCPY.
000460                           WS-EXTRACT-FILE
000470 COPY WLORGCPY.
000480                         STATUS IS WS-EXTRACT-STATUS.
000490     SELECT REPORT-FILE
000500 COPY WVARYCPY.
000510                           WS-REPORT-FILE
000520 COPY WLORGCPY.
000530                         STATUS IS WS-REPORT-STATUS.
000540
000550******************************************************************
000560 DATA DIVISION.
000570
000580******************************************************************
000590 FILE SECTION.
000600
000610 FD  EXTRACT-FILE
000620     DATA RECORD IS EXTRACT-RECORD.
000630 01  EXTRACT-RECORD.
           05  EXT-FORMAT-CODE              PIC X(01).
000650     05  EXT-NAME-ID                  PIC X(08).
000660     05  EXT-ADDRESS-ID               PIC X(08).
000680     05  EXT-EFFECTIVE-DATE           PIC X(08).
000690     05  EXT-ADDRESS-CODE             PIC X(03).
000700     05  EXT-IDENTIFY-NUMBER          PIC X(14).
000710     05  EXT-ADDR-LINE-1              PIC X(105).
000770
000780 FD  REPORT-FILE
000790     DATA RECORD IS REPORT-RECORD.
000800 01  REPORT-RECORD                     PIC X(223).
000810
000820******************************************************************
000830 WORKING-STORAGE SECTION.
000840
000850     COPY WWSCRCPY.
000860
000870 01  PFILEBTV                    PIC X(08) VALUE 'PFILEBTV'.
000880 01  PDATECON                    PIC X(08) VALUE 'PDATECON'.
000890 01  PFILEINS                    PIC X(08) VALUE 'PFILEINS'.
000900 01  PFILERCE                    PIC X(08) VALUE 'PFILERCE'.
000910 01  POPUPERR                    PIC X(08) VALUE 'POPUPERR'.
000920 01  SCREENIO                    PIC X(08) VALUE 'SCRNIO  '.
000930
000940******************************************************************  
000960 01  WS-EXTRACT-FILE.
000970     05  WS-EXTR-DRIVE           PIC X(01) VALUE 'F'.
000980     05  FILLER                  PIC X(01) VALUE ':'.
000990     05  FILLER                  PIC X(12) VALUE 'MUNAME00.EXT'.
001000     05  FILLER                  PIC X(241) VALUE SPACES.
001010
001020 01  WS-REPORT-FILE.
001030     05  WS-REPT-DRIVE           PIC X(01) VALUE 'F'.
001040     05  FILLER                  PIC X(01) VALUE ':'.
001050     05  FILLER                  PIC X(12) VALUE 'MUNAME00.OUT'.
001060     05  FILLER                  PIC X(241) VALUE SPACES.
001070
001080 01  MISC.
001090     05 WS-EXTRACT-STATUS        PIC X(02) VALUE SPACES.
001100     05 WS-REPORT-STATUS         PIC X(02) VALUE SPACES.
001110     05 WS-EXT-SW                PIC X(01) VALUE SPACES.
001120        88 EXT-NOT-DONE            VALUE 'N'.
001130        88 EXT-DONE                VALUE 'Y'.
001140     05  SUB                     PIC S9(09) COMP-5.
001150     05  WS-SUB1                 PIC S9(09) COMP-5.
001160     05  WS-KD                   PIC S9(09) COMP-5.
001170     05 WS-RPT-UPDATE-COUNT      PIC 9(07) VALUE ZEROS.
001180     05 WS-RPT-UPDATE-COUNT-Z REDEFINES WS-RPT-UPDATE-COUNT
001190                                 PIC Z,ZZZ,ZZ9.
001200     05  WS-NAME-MSG              PIC X(44)
001210           VALUE 'Record Updated Successfully In PNAME Table'.
001200     05  WS-ADDR-MSG              PIC X(44)
001210           VALUE 'Record Deleted Successfully From PADDR Table'.
001200     05  WS-NALK-MSG              PIC X(44)
001210           VALUE 'Record Deleted Successfully From PNALK Table'.
001220
001230     05  PROCESSING-FLAG         PIC X(01).
001240         88  PROCESSING-OK           VALUE 'Y'.
001250     05  ERROR-MESSAGE           PIC X(80).
001260     05 WS-UPD-KD-BEN-EXTEND-KEYS
001270                                 PIC X(50).
           05 WS-EXT-NAME-ID           PIC 9(08) COMP-5.
           05 WS-EXT-ADDRESS-ID        PIC 9(08) COMP-5.
001280
001290 01  EXECUTION-FIELDS.
001300     05  WS-CURRENT-DATE         PIC 9(08) VALUE ZEROS.
001310     05  FILLER REDEFINES WS-CURRENT-DATE.
001320         10  WS-CURRENT-YEAR     PIC 9(04).
001330         10  WS-CURRENT-MONTH    PIC 9(02).
001340         10  WS-CURRENT-DAY      PIC 9(02).
001350     05  WS-TIME                 PIC 9(08).
001360     05  FILLER REDEFINES WS-TIME.
001370         10  WS-TIME-HOUR        PIC 9(02).
001380         10  WS-TIME-MIN         PIC 9(02).
001390         10  WS-TIME-SEC         PIC 9(02).
001400         10  WS-TIME-SUBSEC      PIC 9(02).
001410
001420 01 WS-ASCII-FILE-NAME           PIC X(255).
001430 01 WS-NEW-ASCII-FILE-NAME       PIC X(255).
001440
001450 01  POPUPERR-LINKAGE.
001460     05  LIFEPRO-ERROR           PIC 9(4).
001470     05  BTRV-ERROR              PIC 9(5).
001480
001490 01  WS-MESSAGE                     PIC X(80) VALUE SPACES.
001500
001510 01  CONCATENATE-ERROR.
001520     05  FILLER                     PIC X(01) VALUE SPACES.
001530     05  SUBJECT-IN-ERROR           PIC X(05) VALUE SPACES.
001540     05  FILLER                     PIC X(01) VALUE SPACES.
001550     05  KEY-IN-ERROR               PIC X(30) VALUE SPACES.
001560
001570******************************************************************
001580  COPY WBTRVCPY.
001590  COPY WDATECPY.
001600  COPY WFKEYCPY.
001610  COPY WCTRLCPY.
001620  COPY SBNYICPY.
001630  COPY WERRSCPY.
001640
001650  COPY WNAMECPY.
001790  COPY WAPPLCPY REPLACING
001800             WAPPL-CALL-PARAMETERS  BY WNAME-CALL-PARAMETERS
001810             WAPPL-FUNCTION         BY WNAME-FUNCTION
001820             WAPPL-KEY-NUMBER       BY WNAME-KEY-NUMBER
001830             WAPPL-RETURN-CODE      BY WNAME-RETURN-CODE
001840             WAPPL-RETURN-OK        BY WNAME-RETURN-OK
001850             WAPPL-RETURN-NOT-OK    BY WNAME-RETURN-NOT-OK.
        COPY WADDRCPY.
001790  COPY WAPPLCPY REPLACING
001800             WAPPL-CALL-PARAMETERS  BY WADDR-CALL-PARAMETERS
001810             WAPPL-FUNCTION         BY WADDR-FUNCTION
001820             WAPPL-KEY-NUMBER       BY WADDR-KEY-NUMBER
001830             WAPPL-RETURN-CODE      BY WADDR-RETURN-CODE
001840             WAPPL-RETURN-OK        BY WADDR-RETURN-OK
001850             WAPPL-RETURN-NOT-OK    BY WADDR-RETURN-NOT-OK.
        COPY WNALKCPY.
001790  COPY WAPPLCPY REPLACING
001800             WAPPL-CALL-PARAMETERS  BY WNALK-CALL-PARAMETERS
001810             WAPPL-FUNCTION         BY WNALK-FUNCTION
001820             WAPPL-KEY-NUMBER       BY WNALK-KEY-NUMBER
001830             WAPPL-RETURN-CODE      BY WNALK-RETURN-CODE
001840             WAPPL-RETURN-OK        BY WNALK-RETURN-OK
001850             WAPPL-RETURN-NOT-OK    BY WNALK-RETURN-NOT-OK.
        COPY WAPPLCPY.
        COPY WNAAXCPY.
001660  01 WNAME-FILE-FLAG                 PIC X(01).
001670     88 WNAME-END-OF-FILE               VALUE 'Y'.
001680
001690  01 JH-INPUT-TABLE-FLAG                 PIC X(01).
001700     88 WS-INPUT-EOF                    VALUE 'Y'.
001710
001720  01 JH-TABLE-FLAG                   PIC X(01).
001730    88 END-OF-KD-FILE                   VALUE 'Y'.
001740    88 NOT-END-OF-KD-FILE               VALUE 'N'.
001750  01 WS-CURR-POLICY-FLAG             PIC X(01).
001760    88 WS-CURR-POLICY-REC               VALUE 'Y'.
001770    88 WS-NOT-CURR-POLICY-REC           VALUE 'N'.
001780

001860
001870  01  WS-HEADER-LINE-1.
001880     05  PIC X(21) VALUE 'Start Process Date - '.
001890     05  WS-HEAD1-PROCESS-MONTH PIC X(02).
001900     05                         PIC X(01) VALUE '/'.
001910     05  WS-HEAD1-PROCESS-DAY   PIC X(02).
001920     05                         PIC X(01) VALUE '/'.
001930     05  WS-HEAD1-PROCESS-YEAR  PIC X(04).
001940
001950 01  WS-HEADER-LINE-1A.
001960     05  PIC X(21) VALUE 'Start Process Time - '.
001970     05  WS-HEAD1-PROCESS-HOUR  PIC X(02).
001980     05                         PIC X(01) VALUE ':'.
001990     05  WS-HEAD1-PROCESS-MIN   PIC X(02).
002000     05                         PIC X(01) VALUE ':'.
002010     05  WS-HEAD1-PROCESS-SEC   PIC X(02).
002020     05  PIC X(67) VALUE SPACES.
002030
002040 01  WS-HEADER-LINE-2.
002050     05  PIC X(21) VALUE 'End Process Date - '.
002060     05  WS-HEAD2-PROCESS-MONTH PIC X(02).
002070     05                         PIC X(01) VALUE '/'.
002080     05  WS-HEAD2-PROCESS-DAY   PIC X(02).
002090     05                         PIC X(01) VALUE '/'.
002100     05  WS-HEAD2-PROCESS-YEAR  PIC X(04).
002110
002120 01  WS-HEADER-LINE-2A.
002130     05  PIC X(21) VALUE 'End Process Time - '.
002140     05  WS-HEAD2-PROCESS-HOUR  PIC X(02).
002150     05                         PIC X(01) VALUE ':'.
002160     05  WS-HEAD2-PROCESS-MIN   PIC X(02).
002170     05                         PIC X(01) VALUE ':'.
002180     05  WS-HEAD2-PROCESS-SEC   PIC X(02).
002190     05  PIC X(67) VALUE SPACES.
002200
002210 01  WS-HEADER-LINE-3.
002220     05  PIC X(11)  VALUE 'Format Code'.
           05  PIC X(01)  VALUE ';'.
002230     05  PIC X(08)  VALUE 'Name ID'.
           05  PIC X(01)  VALUE ';'.
002240     05  PIC X(08)  VALUE 'Add ID'.
           05  PIC X(01)  VALUE ';'.
002250     05  PIC X(08)  VALUE 'Eff Date'.
           05  PIC X(01)  VALUE ';'.
002260     05  PIC X(08)  VALUE 'Add code'.
           05  PIC X(01)  VALUE ';'.
002270     05  PIC X(14)  VALUE 'Identify No'.
           05  PIC X(01)  VALUE ';'.
002275     05  PIC X(105) VALUE  'ADDR Line 1'.
           05  PIC X(01)  VALUE ';'.
           05  PIC X(44)  VALUE  'Report Msg'.
           05  PIC X(05)  VALUE SPACES.
002290
002300 01  WS-REPORT-RECORD.
002310     05  RPT-FORMAT-CODE          PIC X(01).
002320     05  FILLER                   PIC X(01) VALUE ';'.
002330     05  RPT-NAME-ID              PIC X(08).
           05  FILLER                   PIC X(01) VALUE ';'.
           05  RPT-ADDRESS-ID           PIC X(08).
           05  FILLER                   PIC X(01) VALUE ';'.
           05  RPT-EFFECTIVE-DATE       PIC X(08).
           05  FILLER                   PIC X(01) VALUE ';'.
           05  RPT-ADDRESS-CODE         PIC X(03).
           05  FILLER                   PIC X(01) VALUE ';'.
           05  RPT-IDENTIFY-NUMBER      PIC X(14).
           05  FILLER                   PIC X(01) VALUE ';'.
           05  RPT-ADDR-LINE-1          PIC X(105).
           05  FILLER                   PIC X(01) VALUE ';'.
           05  RPT-MSG                  PIC X(44).
 
002440 78  EXT-MAX-ROWS                 VALUE 4000.
002450
002460 01  WS-SQL-FUNCTION              PIC X(08) VALUE SPACES.
002510
002520     EXEC SQL BEGIN DECLARE SECTION END-EXEC.
002530
002540 01  RECCOUNT                     PIC S9(9) VALUE 0 COMP-5.
002780
002790 01  WSQL-INPUT.
002800     05 WSQL-NAME-FORMAT-CODE     PIC X(01) OCCURS EXT-MAX-ROWS.
002810     05 WSQL-NALK-NAME-ID         PIC X(08) OCCURS EXT-MAX-ROWS.
002820     05 WSQL-NALK-ADDRESS-ID      PIC X(08) OCCURS EXT-MAX-ROWS.
002830     05 WSQL-NALK-EFFECTIVE-DATE  PIC X(08) OCCURS EXT-MAX-ROWS.
002840     05 WSQL-NALK-ADDRESS-CODE    PIC X(03) OCCURS EXT-MAX-ROWS.
           05 WSQL-NALK-COMPANY-CODE    PIC X(02) OCCURS EXT-MAX-ROWS.
002841     05 WSQL-NALK-POLICY-NUMBER   PIC X(12) OCCURS EXT-MAX-ROWS.
002842     05 WSQL-ADDR-ADDR-LINE-1     PIC X(35) OCCURS EXT-MAX-ROWS.
           05 WSQL-ADDR-ADDR-LINE-2     PIC X(35) OCCURS EXT-MAX-ROWS.
           05 WSQL-ADDR-ADDR-LINE-3     PIC X(35) OCCURS EXT-MAX-ROWS.
002850
002860 COPY SQLCA.
002870
002880     EXEC SQL END DECLARE SECTION END-EXEC.
002890
002900******************************************************************
002910 LINKAGE SECTION.
002920
002930 COPY WGLOBCPY.
002940
002950******************************************************************
002960 PROCEDURE DIVISION USING WGLOBAL.
002970
002980******************************************************************
002990 0000-MAINLINE SECTION 01.
003000
003010     IF  NOT PRELOADING-PROGRAM
003020         PERFORM 1000-SELECT-RUN-TYPE THRU
                       1000-SELECT-RUN-TYPE-EXIT
003030                 UNTIL END-KEY OR ENTER-KEY
003040         MOVE 'Y'               TO PROCESSING-FLAG
003050         IF  ENTER-KEY
003060             IF  PROCESSING-OK
003070                 IF SBNYICPY-CURSOR-FIELD = 1
003080                     PERFORM 2000-PROCESS-PARA THRU
                                   2000-PROCESS-PARA-EXIT
003090                 ELSE
003100                     IF SBNYICPY-CURSOR-FIELD = 2
003110                        PERFORM 3000-UPDATE-NAME-REC THRU
                                      3000-UPDATE-NAME-REC-EXIT
003120                     END-IF
003130                 END-IF
003140             END-IF
003150           MOVE ZEROS            TO LIFEPRO-ERROR
003160                                    BTRV-ERROR
003170           IF  PROCESSING-OK
003180               INITIALIZE DATA-AREA
003190               STRING 'Processing Complete'
003200               DELIMITED BY SIZE
003210               INTO DATA-AREA
003220           ELSE
003230              MOVE ERROR-MESSAGE TO DATA-AREA
003240           END-IF
003250               PERFORM 8900-DISPLAY-ERROR
003260         END-IF
003270     END-IF.
003280
003290 0000-MAINLINE-EXIT.
003300     EXIT PROGRAM.
003310
003320******************************************************************
003330*  Initialize program                                            *
003340******************************************************************
003350 0500-INITIALIZE-PARA.
003360
003370     PERFORM 8000-DATE-CALC.
003380
003390     MOVE WS-CURRENT-MONTH      TO WS-HEAD1-PROCESS-MONTH
003400     MOVE WS-CURRENT-DAY        TO WS-HEAD1-PROCESS-DAY
003410     MOVE WS-CURRENT-YEAR       TO WS-HEAD1-PROCESS-YEAR
003420
003430     ACCEPT WS-TIME           FROM TIME
003440
003450     MOVE WS-TIME(1:2)          TO WS-HEAD1-PROCESS-HOUR
003460     MOVE WS-TIME(3:2)          TO WS-HEAD1-PROCESS-MIN
003470     MOVE WS-TIME(5:2)          TO WS-HEAD1-PROCESS-SEC
003480
003490
003500     MOVE WS-EXTRACT-FILE        TO WS-ASCII-FILE-NAME
003510     CALL PFILEINS USING WGLOBAL
003520                         WS-ASCII-FILE-NAME
003530                         WS-NEW-ASCII-FILE-NAME
003540     MOVE WS-NEW-ASCII-FILE-NAME TO WS-EXTRACT-FILE
003550
003560 0500-INITIALIZE-PARA-EXIT.
003570    EXIT.
003590******************************************************************
003600*  Select Run Type                                               *
003610******************************************************************
003620 1000-SELECT-RUN-TYPE.
003630
003640     MOVE '1.Email Address Output' TO SBNYI-SELECT-01
003650     MOVE '2.Email Address Update' TO SBNYI-SELECT-02
003660
003670     MOVE 'C'                    TO SBNYI-SELECT-01-P
003680
003690     CALL SCREENIO USING SBNYICPY-PANEL
003700                         SBNYICPY-PASS-TO-EXIT
003710                         SBNYICPY-WORK-S
003720                         SBNYICPY-WORK-D
003730
003740     MOVE SBNYICPY-EXIT-KEY      TO WS-EXIT-KEY.
003750
003760     CONTINUE.
003770
003780 1000-SELECT-RUN-TYPE-EXIT.
003790     EXIT.
003800
003810******************************************************************
003820*              GET BRUSHFIRE INPUT RECORDS FROM                  *
003830*                NAME,NALK and ADDR Tables                       *
003840******************************************************************
003850 2000-PROCESS-PARA.
003860
003870     PERFORM 0500-INITIALIZE-PARA THRU
003880             0500-INITIALIZE-PARA-EXIT.
003890     OPEN OUTPUT EXTRACT-FILE.
003900
003910     INITIALIZE EXTRACT-RECORD.

003920     MOVE ZERO                   TO  WS-SUB1.
003930
003940         EXEC SQL
003950          SELECT A.NAME_FORMAT_CODE,
                       B.NAME_ID,
                       B.ADDRESS_ID,
003951                 B.EFFECTIVE_DATE,
                       B.ADDRESS_CODE,
                       B.COMPANY_CODE,
                       B.POLICY_NUMBER,
                       C.ADDR_LINE_1,
                       C.ADDR_LINE_2,
                       C.ADDR_LINE_3
                INTO :WSQL-INPUT
004000          FROM PNAME A,PNALK B,PADDR C
004001          WHERE B.ADDRESS_TYPE = 'E'
                AND   A.NAME_ID = B.NAME_ID
                AND   B.ADDRESS_ID = C.ADDRESS_ID
004002          ORDER BY B.EFFECTIVE_DATE
004020        END-EXEC

004860      IF NOT SQLSTATE-OK
004870        MOVE 'JHKDTR02'          TO WS-SQL-FUNCTION
004880        PERFORM 6000-REPORT-SQL-ERROR
004890           THRU 6000-REPORT-SQL-ERROR-EXIT
004900      END-IF.
004910
004100     MOVE WS-HEADER-LINE-1      TO EXTRACT-RECORD
004110     WRITE EXTRACT-RECORD.
004120
004130     INITIALIZE EXTRACT-RECORD
004140     MOVE WS-HEADER-LINE-1A     TO EXTRACT-RECORD
004150     WRITE EXTRACT-RECORD.

007260     INITIALIZE EXTRACT-RECORD
007270     MOVE WS-HEADER-LINE-3      TO EXTRACT-RECORD
007280     WRITE EXTRACT-RECORD.

004920      IF SQLSTATE-OK
004930        MOVE SQLERRD(3)          TO RECCOUNT
004940        IF RECCOUNT NOT  = ZERO
004950           PERFORM VARYING SUB FROM 1 BY 1
004960             UNTIL SUB > RECCOUNT
004970             SET WS-CURR-POLICY-REC TO TRUE
004980             INITIALIZE WNAME-DATA-RECORD
                   PERFORM 2100-WRITE-EXT-FILE
                      THRU 2100-WRITE-EXT-FILE-EXIT
                 END-PERFORM
              END-IF
            END-IF.

004230      PERFORM 8000-DATE-CALC.
004240
004250     MOVE WS-CURRENT-MONTH      TO WS-HEAD2-PROCESS-MONTH
004260     MOVE WS-CURRENT-DAY        TO WS-HEAD2-PROCESS-DAY
004270     MOVE WS-CURRENT-YEAR       TO WS-HEAD2-PROCESS-YEAR
004280
004290     ACCEPT WS-TIME           FROM TIME
004300
004310     MOVE WS-TIME(1:2)          TO WS-HEAD2-PROCESS-HOUR
004320     MOVE WS-TIME(3:2)          TO WS-HEAD2-PROCESS-MIN
004330     MOVE WS-TIME(5:2)          TO WS-HEAD2-PROCESS-SEC
004340
004350     INITIALIZE EXTRACT-RECORD
004360     MOVE WS-HEADER-LINE-2      TO EXTRACT-RECORD
004370     WRITE EXTRACT-RECORD
004380
004390     INITIALIZE EXTRACT-RECORD
004400     MOVE WS-HEADER-LINE-2A     TO EXTRACT-RECORD
004410     WRITE EXTRACT-RECORD.
004420
004430     CLOSE EXTRACT-FILE.

007100 2000-PROCESS-PARA-EXIT.
007110     EXIT.
006220******************************************************************
006230*    WRITE EXTRACT-FILE FOR THE SELECTED RECORDS                **
006240******************************************************************
006250 2100-WRITE-EXT-FILE.
006260
006270     MOVE WSQL-NAME-FORMAT-CODE(SUB)    TO EXT-FORMAT-CODE
006280     MOVE WSQL-NALK-NAME-ID(SUB)        TO EXT-NAME-ID
006290     MOVE WSQL-NALK-ADDRESS-ID(SUB)     TO EXT-ADDRESS-ID
006300     MOVE WSQL-NALK-EFFECTIVE-DATE(SUB) TO EXT-EFFECTIVE-DATE
006310     MOVE WSQL-NALK-ADDRESS-CODE(SUB)   TO EXT-ADDRESS-CODE

006320     STRING WSQL-NALK-COMPANY-CODE(SUB)  DELIMITED BY SIZE
                  WSQL-NALK-POLICY-NUMBER(SUB) DELIMITED BY SIZE
           INTO   EXT-IDENTIFY-NUMBER

006330     STRING WSQL-ADDR-ADDR-LINE-1(SUB) DELIMITED BY SIZE
                  WSQL-ADDR-ADDR-LINE-2(SUB) DELIMITED BY SIZE
                  WSQL-ADDR-ADDR-LINE-3(SUB) DELIMITED BY SIZE
           INTO EXT-ADDR-LINE-1

006430     WRITE EXTRACT-RECORD.

006490
006500 2100-WRITE-EXT-FILE-EXIT.
006510     EXIT.
006520******************************************************************
006530**       READ EXTRACT-FILE AND UPDATE PNAME TABLE               **
006550******************************************************************
006570 3000-UPDATE-NAME-REC.

006580     PERFORM 0500-INITIALIZE-PARA THRU
                   0500-INITIALIZE-PARA-EXIT.

006590     INITIALIZE EXTRACT-RECORD.
006600     OPEN INPUT EXTRACT-FILE.

006610     MOVE WS-REPORT-FILE         TO WS-ASCII-FILE-NAME
006620     CALL PFILEINS USING WGLOBAL
006630                         WS-ASCII-FILE-NAME
006640                         WS-NEW-ASCII-FILE-NAME
006650     MOVE WS-NEW-ASCII-FILE-NAME TO WS-REPORT-FILE
006660
006670     OPEN OUTPUT REPORT-FILE.
006680     INITIALIZE WS-REPORT-RECORD
006690                REPORT-RECORD.
006700
006710     PERFORM 3100-PAGE-HEADER THRU
                   3100-PAGE-HEADER-EXIT.
006720
006730     READ EXTRACT-FILE NEXT RECORD
006740         AT END MOVE 'Y'         TO WS-EXT-SW.
006750
006760     PERFORM 3200-UPDATE-NAME-TABLE
006770        THRU 3200-UPDATE-NAME-TABLE-EXIT
006780        UNTIL EXT-DONE

006800     PERFORM 8000-DATE-CALC
006810
006820     MOVE WS-CURRENT-MONTH      TO WS-HEAD2-PROCESS-MONTH
006830     MOVE WS-CURRENT-DAY        TO WS-HEAD2-PROCESS-DAY
006840     MOVE WS-CURRENT-YEAR       TO WS-HEAD2-PROCESS-YEAR
006850
006860     ACCEPT WS-TIME           FROM TIME
006870
006880     MOVE WS-TIME(1:2)          TO WS-HEAD2-PROCESS-HOUR
006890     MOVE WS-TIME(3:2)          TO WS-HEAD2-PROCESS-MIN
006900     MOVE WS-TIME(5:2)          TO WS-HEAD2-PROCESS-SEC
006910
006920     INITIALIZE WS-REPORT-RECORD
006930     MOVE WS-HEADER-LINE-2      TO REPORT-RECORD
006940     WRITE REPORT-RECORD
006950
006960     INITIALIZE WS-REPORT-RECORD
006970     MOVE WS-HEADER-LINE-2A     TO REPORT-RECORD
006980     WRITE REPORT-RECORD
006990
007000     INITIALIZE WS-REPORT-RECORD
007010     STRING 'TOTAL NUMBER OF RECORDS UPDATED - '
007020             WS-RPT-UPDATE-COUNT-Z
007030            DELIMITED BY SIZE
007040            INTO WS-REPORT-RECORD
007050     WRITE REPORT-RECORD       FROM WS-REPORT-RECORD.
007060     CLOSE EXTRACT-FILE
007070           REPORT-FILE.

007100 3000-UPDATE-NAME-REC-EXIT.
007110     EXIT.
007130******************************************************************
007140*                   Process Report Header                        *
007150******************************************************************
007160 3100-PAGE-HEADER.
007170
007190     MOVE WS-HEADER-LINE-1      TO REPORT-RECORD
007200     WRITE REPORT-RECORD
007210
007220     INITIALIZE WS-REPORT-RECORD
007230     MOVE WS-HEADER-LINE-1A     TO REPORT-RECORD
007240     WRITE REPORT-RECORD
007250
007260     INITIALIZE WS-REPORT-RECORD
007270     MOVE WS-HEADER-LINE-3      TO REPORT-RECORD
007280     WRITE REPORT-RECORD.
007290
007300     CONTINUE.
007310
007320 3100-PAGE-HEADER-EXIT.
007330     EXIT.
007130******************************************************************
007140*                   Update Email Address Of Name Table           *
007150******************************************************************
007350 3200-UPDATE-NAME-TABLE.

           COMPUTE WS-EXT-NAME-ID = FUNCTION NUMVAL(EXT-NAME-ID)

           IF EXT-NAME-ID EQUAL TO SPACES
           OR WS-EXT-NAME-ID EQUAL TO ZERO
              NEXT SENTENCE
           ELSE
              MOVE WS-EXT-NAME-ID         TO WNAME-NAME-ID
081699        MOVE WBTRV-GET-EQUAL        TO WAPPL-FUNCTION
081699        MOVE ZEROES                 TO WAPPL-KEY-NUMBER
081699        PERFORM 4000-ACCESS-NAME THRU
                      4000-ACCESS-NAME-EXIT
005080        IF WBTRV-SUCCESSFUL
005100          IF WNAME-NAME-FORMAT-CODE = 'I'
                  MOVE EXT-ADDR-LINE-1   TO WNAME-PERSONAL-EMAIL-ADR
                ELSE
                  MOVE EXT-ADDR-LINE-1   TO WNAME-BUSINESS-EMAIL-ADR
                END-IF
                MOVE WBTRV-UPDATE        TO WAPPL-FUNCTION
                PERFORM 4000-ACCESS-NAME THRU
                        4000-ACCESS-NAME-EXIT
007550          IF WBTRV-SUCCESSFUL
007560             ADD 1 TO WS-RPT-UPDATE-COUNT
007570             MOVE EXT-NAME-ID      TO RPT-NAME-ID
007590             MOVE EXT-FORMAT-CODE  TO RPT-FORMAT-CODE
007600             MOVE EXT-ADDRESS-ID   TO RPT-ADDRESS-ID
                   MOVE EXT-EFFECTIVE-DATE
                                         TO RPT-EFFECTIVE-DATE
                   MOVE EXT-ADDRESS-ID   TO RPT-ADDRESS-ID
                   MOVE EXT-IDENTIFY-NUMBER
                                         TO RPT-IDENTIFY-NUMBER
                   MOVE EXT-ADDR-LINE-1  TO RPT-ADDR-LINE-1
007650             MOVE WS-NAME-MSG       TO RPT-MSG
007660             WRITE REPORT-RECORD  FROM WS-REPORT-RECORD
                   PERFORM 3210-DEL-ADDR-RECORD THRU
                           3210-DEL-ADDR-RECORD-EXIT
007680          END-IF
007690        END-IF
           END-IF.
007710     READ EXTRACT-FILE NEXT RECORD
007720         AT END MOVE 'Y'         TO WS-EXT-SW.

007740 3200-UPDATE-NAME-TABLE-EXIT.
007750    EXIT
007130******************************************************************
007140*    Delete Corresponding ADDR table record                      *
007150******************************************************************
007350 3210-DEL-ADDR-RECORD.

           COMPUTE WS-EXT-ADDRESS-ID = FUNCTION NUMVAL(EXT-ADDRESS-ID)

           MOVE WS-EXT-ADDRESS-ID      TO WADDR-ADDRESS-ID
081699     MOVE WBTRV-GET-EQUAL        TO WAPPL-FUNCTION
081699     MOVE ZEROES                 TO WAPPL-KEY-NUMBER
           PERFORM 3215-ACCESS-ADDR THRU
                   3215-ACCESS-ADDR-EXIT
005080     IF WBTRV-SUCCESSFUL
              MOVE WBTRV-DELETE        TO WAPPL-FUNCTION
              PERFORM 3215-ACCESS-ADDR THRU
                      3215-ACCESS-ADDR-EXIT
007550        IF WBTRV-SUCCESSFUL
007560           ADD 1 TO WS-RPT-UPDATE-COUNT
007570           MOVE EXT-NAME-ID      TO RPT-NAME-ID
007590           MOVE EXT-FORMAT-CODE  TO RPT-FORMAT-CODE
007600           MOVE EXT-ADDRESS-ID   TO RPT-ADDRESS-ID
                 MOVE EXT-EFFECTIVE-DATE
                                       TO RPT-EFFECTIVE-DATE
                 MOVE EXT-ADDRESS-ID   TO RPT-ADDRESS-ID
                 MOVE EXT-IDENTIFY-NUMBER
                                       TO RPT-IDENTIFY-NUMBER
                 MOVE EXT-ADDR-LINE-1  TO RPT-ADDR-LINE-1
007650           MOVE WS-ADDR-MSG      TO RPT-MSG
007660           WRITE REPORT-RECORD  FROM WS-REPORT-RECORD
                 PERFORM 3220-DEL-NALK-RECORD THRU
                         3220-DEL-NALK-RECORD-EXIT
              END-IF
           END-IF.

007740 3210-DEL-ADDR-RECORD-EXIT.
007750    EXIT
007130******************************************************************
007140*    Access ADDR Record                                          *
007150******************************************************************
007770 3215-ACCESS-ADDR.
007780
007810     MOVE WADDR-DATA-RECORD      TO DATA-AREA.
007820     MOVE WFILE-ADDR-NUM         TO DATA-FILE-NUMBER.
007830
007840     PERFORM 5000-CALL-BTRV
007850        THRU 5000-CALL-BTRV-EXIT.
007860
007870     MOVE DATA-AREA              TO WADDR-DATA-RECORD.
007880
007890 3215-ACCESS-ADDR-EXIT.
007900     EXIT.
007130******************************************************************
007140*    Delete Corresponding NALK table record                      *
007150******************************************************************
007350 3220-DEL-NALK-RECORD.

           MOVE WS-EXT-NAME-ID         TO WNALK-NAME-ID
           MOVE EXT-EFFECTIVE-DATE     TO WNALK-EFFECTIVE-DATE
           MOVE EXT-ADDRESS-CODE       TO WNALK-ADDRESS-CODE
           MOVE EXT-IDENTIFY-NUMBER    TO WNALK-IDENTIFYING-NUMBER
           MOVE WS-EXT-ADDRESS-ID      TO WNALK-ADDRESS-ID
081699     MOVE WBTRV-GET-EQUAL        TO WAPPL-FUNCTION
081699     MOVE ZEROES                 TO WAPPL-KEY-NUMBER
           PERFORM 3225-ACCESS-NALK THRU
                   3225-ACCESS-NALK-EXIT
005080     IF WBTRV-SUCCESSFUL
              MOVE WBTRV-DELETE        TO WAPPL-FUNCTION
              PERFORM 3225-ACCESS-NALK THRU
                      3225-ACCESS-NALK-EXIT
007550          IF WBTRV-SUCCESSFUL
007560             ADD 1 TO WS-RPT-UPDATE-COUNT
007570             MOVE EXT-NAME-ID      TO RPT-NAME-ID
007590             MOVE EXT-FORMAT-CODE  TO RPT-FORMAT-CODE
007600             MOVE EXT-ADDRESS-ID   TO RPT-ADDRESS-ID
                   MOVE EXT-EFFECTIVE-DATE
                                         TO RPT-EFFECTIVE-DATE
                   MOVE EXT-ADDRESS-ID   TO RPT-ADDRESS-ID
                   MOVE EXT-IDENTIFY-NUMBER
                                         TO RPT-IDENTIFY-NUMBER
                   MOVE EXT-ADDR-LINE-1  TO RPT-ADDR-LINE-1
007650             MOVE WS-NALK-MSG      TO RPT-MSG
007660             WRITE REPORT-RECORD  FROM WS-REPORT-RECORD
                END-IF
           END-IF.

007740 3220-DEL-NALK-RECORD-EXIT.
007750    EXIT
007130******************************************************************
007140*    Access NALK Record                                          *
007150******************************************************************
007770 3225-ACCESS-NALK.
007780
007810     MOVE WNALK-DATA-RECORD      TO DATA-AREA.
007820     MOVE WFILE-NALK-NUM         TO DATA-FILE-NUMBER.
007830
007840     PERFORM 5000-CALL-BTRV
007850        THRU 5000-CALL-BTRV-EXIT.
007860
007870     MOVE DATA-AREA              TO WNALK-DATA-RECORD.
007880
007890 3225-ACCESS-NALK-EXIT.
007900     EXIT.
007130******************************************************************
007140*    Access Name Record                                          *
007150******************************************************************
007770 4000-ACCESS-NAME.
007800
007810     MOVE WNAME-DATA-RECORD      TO DATA-AREA.
007820     MOVE WFILE-NAME-NUM         TO DATA-FILE-NUMBER.
007830
007840     PERFORM 5000-CALL-BTRV
007850        THRU 5000-CALL-BTRV-EXIT.
007860
007870     MOVE DATA-AREA              TO WNAME-DATA-RECORD.
007880
007890 4000-ACCESS-NAME-EXIT.
007900     EXIT.
007340*****************************************************************
007920 5000-CALL-BTRV.
007930
007940     CALL PFILEBTV
007950         USING WGLOBAL,
007960               WAPPL-CALL-PARAMETERS,
007970               WBTRIEVE
007980     END-CALL.
007990
008000 5000-CALL-BTRV-EXIT.
008010     EXIT.
008020
008030 8000-DATE-CALC.
008040     SET WDATE-ACCEPT-SYSTEM-DATE TO TRUE
008050     CALL PDATECON USING WDATE-PARMS.
008060
008070     MOVE 'C'                   TO WDATE-FUNCTION
008080
008090     CALL PDATECON USING WDATE-PARMS.
008100     MOVE WDATE-STR-GREG-DATE   TO WS-CURRENT-DATE.
008110
008120 8000-DATE-CALC-EXIT.
008130     EXIT.
008140
008150
008160******************************************************************
008170*     Pass SQL errors to LifePRO Critical Error Report
008180******************************************************************
008190 6000-REPORT-SQL-ERROR.
008200
008210     INITIALIZE                     SQLCA-ERROR-PARAMETERS
008220     MOVE 9999                   TO WS-ERROR-FILE-NUMBER
008230                                    WS-ERROR-FUNCTION
008240     MOVE SQLMSG                 TO WS-ERROR-SQLMSG
008250     MOVE SQLSTATE               TO WS-ERROR-RETURN-CODE
008260     MOVE WS-SQL-FUNCTION        TO WS-ERROR-SQL-FUNCTION
008270     CALL PFILERCE USING WGLOBAL
008280                         SQLCA-ERROR-PARAMETERS
008290     CANCEL PFILERCE
008300     MOVE SPACES                 TO WS-SQL-FUNCTION
008310
008320     CONTINUE.
008330 6000-REPORT-SQL-ERROR-EXIT.
008340     EXIT.
008350
008360 8900-DISPLAY-ERROR.
008370
008380     CALL POPUPERR USING WGLOBAL
008390                         LIFEPRO-ERROR
008400                         BTRV-ERROR.
008410
008420     CONTINUE.
008430 8900-DISPLAY-ERROR-EXIT.
008440     EXIT.
008450
008460******************************************************************
008470*                    E N D   O F   S O U R C E                   *
008480******************************************************************
008490
